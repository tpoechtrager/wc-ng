newgui main [
    guilist [
        guitext "name: " (playermodelicon)
        newname = (getname)
        guifield newname 15 [name $newname]
    ]
    guibutton (concat "model:" (playermodelname)) [guirolloveraction = (playermodelbutton $playermodel); showgui playermodel] (playermodelicon)
    guilist [
        guibutton "crosshair: " [showgui crosshair] (playermodelicon)
        guiimage (getcrosshair) [showgui crosshair] 0.5
    ]
    guibar
    guibutton "server browser.."  "showgui servers"
    if (isconnected) [
        guibar
        if (|| $editing (m_edit (getmode))) [
            guibutton "editing.." "showgui editing"
        ]
        guibutton "vote game mode / map.." "showgui gamemode"
        // guibutton "lanconnect"
        // guibutton "connect localhost"
        guibutton "switch team" [if (strcmp (getteam) "good") [team evil] [team good]]
        guibutton "toggle spectator" [spectator (! (isspectator (getclientnum)))] "spectator"
        guibutton "master.." [showgui master]
        guibar
        guibutton "wc-ng settings.."    "f_show_wc_ng_settings"
        guibar
        guibutton "options.."  "showgui options"
        guibutton "disconnect" "disconnect"         "exit"
    ] [
        guibutton "bot match.."      "showgui botmatch"
        guibutton "campaign.."       "showgui campaign"
        guibar
        guibutton "wc-ng settings.."    "f_show_wc_ng_settings"
        guibar
        guibutton "options.."        "showgui options"
        guibutton "about.."          "showgui about"
        guibutton "quit"             "quit"                 "exit"
    ]
] 0


if ( = (checkwcng) 1) [

newgui "geoip database update" [
    local total
    local downloaded
    local time

    total = (divf $geoip_update_totalsize 1024)
    downloaded = (divf $geoip_update_downloaded 1024)
    time = (divf $geoip_update_time_elapsed 1000)

    guitext "Downloading geoip database..."

    guibar

    guitext (format "Size: %1 kb" (roundf $total 2))
    guitext (format "Downloaded: %1 kb (%2%%)" (roundf $downloaded 2) $geoip_update_percent_done)
    guitext (format "Time Elapsed: %1 seconds" (roundf $time 2))

    guibar

    guialign 0 [
        guibutton "close" [hidegui "geoip database update"]
    ]
]

geoip_download_status_update = [
    geoip_update_totalsize = $arg1
    geoip_update_downloaded = $arg2
    geoip_update_percent_done = $arg3
    geoip_update_time_elapsed = $arg4
]

show_geoip_download_status_gui = [
    showgui "geoip database update"
]

// GUI extensions

createine "showserverfilteroptions" 0
createine "showservermodfilters" 0

serverfilter = [
    guibar

    if ($showserverfilteroptions) [
        guialign 0 [
            guitext "server description " 0
            guifield filterserverdesc 15 []
            guibar
            guitext "game mode " 0
            guifield filterservermodestring 15 []
            guibar
            guitext "country " 0
            guifield filterservercountry 2 []
        ]
    ]

    if ($showservermodfilters) [
        if ($showserverfilteroptions) [ guibar ]
        guialign 0 [
            guitext "server mod" 0
            guibar
            guiradio "oo|mod" filterservermod -3
            guibar
            guiradio "hopmod" filterservermod -2
            guibar
            guiradio "spaghettimod" filterservermod -4
            guibar
            guiradio "suckerserv" filterservermod -5
            guibar
            guiradio "remod" filterservermod -6
            guibar
            guiradio "noobmod" filterservermod -7
            guibar
            guiradio "zeromod" filterservermod -8
            guibar
            guiradio "any" filterservermod 0
        ]
    ]
]

newgui servers [
    guistayopen [
        guiservers [
            guilist [
                guibutton "update from master server" "updatefrommaster"
                guibar
                guibutton "find players" [showgui $extinfo_player_search_guiname]
                guibar
                guicheckbox "search LAN" searchlan
                guibar
                guicheckbox "auto-update" autoupdateservers
                guibar
                guicheckbox "auto-sort" autosortservers
                if (= $autosortservers 0) [
                    guibar
                    guibutton "sort" "sortservers"
                ]
                guibar
                guicheckbox "filter server" showserverfilteroptions 1 0 [ filterserverdesc ""; filterservermodestring ""; filterservercountry "" ]
                guibar
                guicheckbox "filter server mod" showservermodfilters 1 0 [ filterservermod 0 ]
            ]
            if (= (checkwcng) 1) [
                if (|| ($showserverfilteroptions) ($showservermodfilters)) [ serverfilter ]
            ]
            guibar
        ] 17
    ]
] "" [initservers]

]

f_show_wc_ng_settings = [
    if (= (checkwcng) 1) [
        showgui "wc-ng settings 1"
    ] [
        showgui "WCError"
    ]
]

newgui "WCError" [
    guitext "^f3ERROR:" 0
    guitext "You are not using WC-NG >= 0.7.3." 0
    guitext "Get it at http://wc-ng.sauerworld.org" 0
]

newgui "wc-ng settings 1" [
    f_show_wc_ng_menu
]
