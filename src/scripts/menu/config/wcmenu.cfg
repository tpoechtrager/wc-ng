if (= (checkwcng) 1) [

loop i (listlen $wc_plugins) [
    createine (concatword "plugin_" (f_get_plugin_name $i) "_autoload") 0
]

newgui "Player Display Settings" [
    guistayopen [
        guilist [
            guicheckbox "enabled" showplayerdisplay
            guibar
            guibutton "reset" "playerdisplayreset"
        ]
        guibar
        guicheckbox "show spectators" playerdisplayshowspectators
        guibar
        guitext "width offset (default: 0)"
        guislider playerdisplaywidthoffset
        guitext "height offset (default: 250)"
        guislider playerdisplayheightoffset
        guitext "right offset (default: 0)"
        guislider playerdisplayrightoffset
        guitext "line offset (default: 0)"
        guislider playerdisplaylineoffset
        guitab 2
        guitext "player limit (default: 32)"
        guislider playerdisplayplayerlimit
        guitext "max player name len (default: 13)"
        guislider playerdisplaymaxnamelen
        guitext "alpha (default: 150)"
        guislider playerdisplayalpha
    ]
]

playerdisplaysettings = [ showgui "Player Display Settings" ]

newgui "Scoreboard Settings" [
    guistayopen [
        guicheckbox "Show Extinfo"            "showextinfo"
        guibar
        guicheckbox "Show Frags"              "showfrags"
        guicheckbox "Show Deaths"             "showdeaths"
        guicheckbox "Show KpD"                "showkpd"
        guicheckbox "Show Accuracy"           "showacc"
        guicheckbox "Show Teamkills"          "showtks"
        guicheckbox "Show Ping Of Spectators" "showspectatorping"
        guibar
        guitext     "Show Country"
        guislider   "showcountry"
        guibar
        guitext     "Show Damage Dealt"
        guislider   "showdamagedealt"
        guibar
        guicheckbox "Show Server Uptime"      "showserveruptime"
        guicheckbox "Show Server Mod Name"    "showservermod"
    ]
]

extinfosettings = [ showgui "Scoreboard Settings" ]

statsdisplaysettings = [
    f_color_format_choice $statsdisplayfmt 6 [
        "Name"           "%n"
        "Team"           "%t"
        "Teamkills"      "%i"
        "Frags"          "%f"
        "KpD"            "%k"
        "Accuracy"       "%a"
        "Ping"           "%p"
        "State"          "%s"
        "Flags / Skulls" "%x"
        "Game mode"      "%g"
        "Game speed"     "%u"
        "Game Paused"    "%l"
        "Map name"       "%m"
        "Map info"       "%o"
        "Mastermode"     "%e"
        ] [ if (> $numargs 0) [ statsdisplayfmt $arg1 ]
    ]
]

f_detect_proxy_users_info = [
    newgui "proxy detection info" [
        guistayopen [
            guitext "Detect proxy users by checking their ips against several proxy lists and mark"
            guitext "them with a special background color (black by default)"
            guitext "in the scoreboard / extinfo preview window."
            guitext "This feature only works on servers where no extinfo ip tampering is done."
            guitext "Some servers are blacklisted to avoid false positives."
        ]
    ]

    showgui "proxy detection info"
]

f_show_wc_ng_menu = [
    guistayopen [
    guititle "General Configuration"

    guilist [
        guilist [
            guicheckbox "Enable Chat Colors"                    "chatcolors"
            guilist [
                guicheckbox "Show Stats Display"                "showstatsdisplay"
                guibar
                guibutton   "Settings"                          "statsdisplaysettings"
            ]
            guicheckbox "Show Time Left Display"                "showtimeleft"
            guicheckbox "Show Ping Display"                     "showpingdisplay"
            guicheckbox "Show Bandwidth Display"                "showbandwidthdisplay"
            guilist [
                guicheckbox "Show Player Display"               "showplayerdisplay"
                guibar
                guibutton   "Settings"                          "playerdisplaysettings"
            ]
            guilist [
                guicheckbox "Show Extinfo Scoreboard"           "showextinfo"
                guibar
                guibutton   "Settings"                          "extinfosettings"
            ]
            guilist [
                guicheckbox "Check for new versions on startup" "wcautoversioncheck"
                guibar
                guibutton   "Check now"                         "wccheckversion"
            ]
        ]
    ]

    guititle "Plugins";

    f_update_plugin_status

    guilist [
        printplugins
    ]

    guilist [
        guibutton "^f0List Plugins "              "listloadedplugins"
    ]

    guitab "wc-ng settings 2"

    guititle  "Misc"
    guibutton "Global Chat"                       [showgui $gchat_gui_name]
    guibutton "HW Temp Display"                   [showgui $hwtemp_gui_name]
    guibar

    guititle    "Extinfo"
    guicheckbox "Use Extinfo Server Browser"      extinfoserverbrowser
    guicheckbox "Show Teams In Horizontal Order"  extinfoguihorizontal
    guibutton   "Find Players"                    [showgui $extinfo_player_search_guiname]
    guibar

    guititle  "Demo"
    guibutton "Demo Recorder"                     "demorecordergui"
    guibutton "Search Demo"                       "searchdemogui"

    guitab "wc-ng settings 3"

    guititle    "Proxy Detection"
    guicheckbox "Detect Proxy Users"              "detectproxyusers"
    guilist [
        guitext "Proxy Background Color"
        guibar
        guicolorfield proxybgcolor
    ]
    guibutton "Info"                              f_detect_proxy_users_info

    ]// end guistayopen
]

printplugins = [
    len = (listlen $wc_plugins)

    guilist [
        loop i $len [
            guitext (concatword (f_get_plugin_desc $i) " ")
        ]
    ]

    guilist [
        loop i $len [
            guicheckbox "Enabled " (concatword "plugin_" (f_get_plugin_name $i) "_loaded") [] [] [ f_update_plugins ]
        ]
    ]

    guilist [
        loop i $len [
            guicheckbox "AutoLoad " (concatword "plugin_" (f_get_plugin_name $i) "_autoload")
        ]
    ]

    guilist [
        loop i $len [
            if (getalias (concatword (f_get_plugin_name $i) "gui")) [
                guibutton "Gui " (concatword (f_get_plugin_name $i) "gui")
            ] [
                guitext "" 0
            ]
        ]
    ]

    guilist [
        loop i $len [
            if (getalias (concatword (f_get_plugin_name $i) "settings")) [
                guibutton "Settings " (concatword (f_get_plugin_name $i) "settings")
            ] [
                guitext "" 0
            ]
        ]
    ]
]

gui_render_country = [
    local desc
    local x
    local end
    local sv_country_codes
    local sv_country_names
    local clickaction

    desc = $arg1
    x = $arg2
    end = $arg3
    sv_country_codes = $arg4
    sv_country_names = $arg5
    clickaction = $arg6

    if ($showcountry) [
        guilist [
            guititleleftalign $desc
            while [(< $x $end)] [
                local icon
                local countrycode
                local country

                icon = 0
                country = (scriptvector $sv_country_names $x)
                countrycode = (scriptvector $sv_country_codes $x)

                if (>= $showcountry 3) [
                    icon = (countryflagpath $countrycode $country)
                    if (=s $icon "") [ icon = "UNKNOWN.png" ]
                ]

                if (< $showcountry 5) [
                    if (> (mod $showcountry 2) 0) [
                        country = $countrycode
                    ]
                ]

                guistayopen [
                    if (> (strlen $clickaction) 0) [
                        guibutton $country [@clickaction @countrycode] $icon
                    ] [
                        guitext $country $icon
                    ]
                ]

                x = (+ $x 1)
            ]

            guistrut (? (! (mod $showcountry 2)) 16 8) 1
        ]
    ]
]

gui_render_server = [
    local desc
    local x
    local end
    local sv_server_names
    local sv_server_addresses

    desc = $arg1
    x = $arg2
    end = $arg3
    sv_server_names = $arg4
    sv_server_addresses = $arg5

    local address
    local ipaddr
    local port

    guilist [
        guititleleftalign $desc
        while [(< $x $end)] [
            address = (scriptvector $sv_server_addresses $x)
            if (! (=s $address "0.0.0.0 0")) [
                ip = (at $address 0)
                port = (at $address 1)
                guibutton (scriptvector $sv_server_names $x) [
                    if ($extinfoserverbrowser) [
                        extinfoconnect @@ip @@port
                    ] [
                        connect @@ip @@port
                    ]
                ] 0
            ] [
               guitext "- not playing -" 0
            ]
            x = (+ $x 1)
        ]
        guistrut 27 1
    ]
]

] // end check wc
